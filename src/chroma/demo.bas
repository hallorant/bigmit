Highspeed BASIC named 'D' ok
1 GOTO60000
10 PRINT"SELECT:  1) RANDOM LINES            (WITH RANDOM SOUNDS)         2) SLOW PONG            (GAME PROGRAM)         3) DRAWING PROGRAM            (DRAW YOUR OWN             HI-RES PICTURES)"
15 PRINT:PRINT"         4) INCORRECT DRIVER"
16 PRINT"            VERSION ASSUMED"
17 PRINT"            ABOVE"
20 A$=INKEY$:IFA$=""THEN20ELSEA=VAL(A$):IFA<1ORA>4THENRUN
21 CLS:IFA=4THEN60220
25 LB=6:GOSUB20000:LB=30:GOSUB20000
30 ON A GOTO 100,1000,30000,60220
40 RUN
100 CLS
110 PRINT"PRESS ENTER FOR MENU"
200 POKE30518-O,RND(255):POKE30519-O,RND(14):GOSUB10000:GOSUB10500
300 X=30522-O:Y=30524-O:X1=30526-O:Y1=30527-O
400 XC=RND(255):YC=RND(191)
500 FORYA=1TO20
510 IFINKEY$=CHR$(13)THENRUN
600 XP=RND(255):YP=RND(191)
700 Z=PEEK(30518-O):Z=ZAND15:Z=ZOR(RND(15)*16):POKE30518-O,Z:POKEX,XC:POKEY,YC:POKEX1,XP:POKEY1,YP:GOSUB10100
800 POKE30571-O,RND(255):POKE30572-O,RND(25):POKE30570-O,RND(100):GOSUB10200
900 XC=XP:YC=YP:NEXT:GOTO100
1000 CLS' SUPER PONG GAME
1001 CLS:PRINT"This is a slow version of the popular PONG game."
1002 PRINT:PRINT"If you have Atari game paddles, plug them into your":PRINT"CHROMAtrs in the right joystick jack (looking at the rear).":PRINT"If you do not have Atari paddles, the game may be controlled":PRINT"from the keyboard."
1003 PRINT:PRINT"The rules are not published, finding them out is half":PRINT"the fun of the game!":PRINT:PRINT:PRINT"Press ENTER to begin the game"
1004 IFINKEY$<>CHR$(13)THEN1004
1010 'DEFINE SPRITES
1020 POKE30570-O,30:POKE30571-O,32:POKE30572-O,10
1030 INPUT"ENTER 0 FOR KEYBOARD OR 1 FOR PADDLES";PD
1035 INPUT"SKILL LEVEL (1-10)";VX:IFVX>10ORVX<1THEN1035ELSEVY=5:VX=VX*4
1037 INPUT"HOW MANY PLAYERS (1-2)";PL:IFPL>2ORPL<1THEN1037
1040 POKE30518-O,26:POKE30519-O,10:GOSUB10000
1050 FORA=30529-OTO30561-O:POKEA,0:NEXT
1060 FORA=30546-OTO30561-O:POKEA,15:NEXT
1070 POKE30561-O,0:LB=21:GOSUB20000' DEFINE SPRITE PATTERN (PADDLE)
1080 RESTORE:FORA=30529-OTO30560-O:READB:POKEA,B:NEXT
1090 DATA7,8,16,38,72,198,193,206,192,198,200,72,38,16,8,7,224,16,8,52,66,51,11,115,3,51,67,66,52,8,16,224
1100 POKE30561-O,1:LB=21:GOSUB20000' DEFINE SPRITE PATTERN (BALL)
1110 POKE30564-O,208:POKE30561-O,3:LB=27:GOSUB20000:' ONLY 3 SPRITES ON SCREEN
1120 POKE30561-O,2:POKE30562-O,4:POKE30528-O,12:LB=24:GOSUB20000: 'SPRITE DEFINITION (BALL, GREEN, SPRITE 2)
1130 POKE30561-O,0:POKE30562-O,0:POKE30528-O,6:LB=24:GOSUB20000: 'SPRITE DEFINITION (LEFT PADDLE, RED, SPRITE 0)
1140 POKE30561-O,1:POKE30562-O,0:POKE30528-O,13:LB=24:GOSUB20000: 'SPRITE DEFINITION (RT PADDLE, VIOLET, SPRITE 1)
2000 XX=127:YY=RND(100)+50
2999 POKE30518-O,74
3000 READA:READB:READC:READD:IFA<256THENPOKE30522-O,A:POKE30524-O,B:POKE30526-O,C:POKE30527-O,D:LB=33:GOSUB20000:GOTO3000
3005 POKE30518-O,26
3500 DATA 22,10,236,10,236,10,236,186,236,186,22,186,22,186,22,10,27,10,27,186,231,10,231,186,129,10,129,186,129,98,120,10,129,98,139,10,129,98,139,186,129,98,120,186,139,10,139,186,119,10,119,186,999,0,0,0
5000 GOSUB6000  'MOVE PADDLE TO CORRECT POSITION
5010 GOSUB7000  'MOVE BALL TO NEXT POSITION
5020 GOSUB8000  'CHECK FOR DEFLECTIONS
5030 GOTO5000
6000 FORLL=1TO2:IFPD=0THEN6500ELSEPOKE30573-O,0:LB=48:GOSUB20000:P0=INT(PEEK(30573-O)/1.5)
6010 POKE30573-O,1:LB=48:GOSUB20000:P1=INT(PEEK(30573-O)/1.5)
6012 IFPL=1THENP1=YY+(RND(10)-10):GOTO6015
6015 GOSUB6020:NEXT:RETURN
6020 POKE30564-O,P0:POKE30563-O,10:POKE30561-O,0:LB=27:GOSUB20000
6030 POKE30564-O,P1:POKE30563-O,220:POKE30561-O,1:LB=27:GOSUB20000
6040 RETURN
6500 FORLL=1TO2:KB=PEEK(14400):IF(KBAND8)THENP0=P0-5
6510 IF(KBAND16)THENP0=P0+8
6515 IFPL=1THENP1=YY+(RND(10)-10):GOTO6015
6520 IF(KBAND32)THENP1=P1-5
6530 IF(KBAND64)THENP1=P1+8
6540 IFP0>170THENP0=170
6550 IFP0<1THENP0=1
6560 IFP1>170THENP1=170
6570 IFP1<1THENP1=1
6600 GOSUB6020:NEXT:RETURN
7000 XX=XX+VX:YY=YY+VY:IFXX>216THENXX=216:DX=1:ELSEIFXX<25THENXX=25:DX=1
7005 IFYY>170THENYY=170:DY=1:ELSEIFYY<10THENYY=10:DY=1
7010 POKE30563-O,XX:POKE30564-O,YY:POKE30561-O,2:LB=27:GOSUB20000:RETURN
8000 IFDY=1THENDY=0:VY=0-VY:GOSUB10200
8010 IFDX=0THENRETURNELSEDX=0
8012 IFXX>100THENPP=P1:PT=1:ELSEPP=P0:PT=0
8014 IFYY>PPANDYY<PP+8THENVY=4:SC(PT)=SC(PT)+1:ELSEIFYY>PPANDYY<PP+19THENVY=20:SC(PT)=SC(PT)+2
8015 IFYY<PPANDYY>PP-8THENVY=0-4:SC(PT)=SC(PT)+1:ELSEIFYY<PPANDYY>PP-19THENVY=0-20:SC(PT)=SC(PT)+2
8017 POKE30520-O,90:POKE30521-O,2:POKE30565-O,SC(0):LB=42:GOSUB20000:POKE30565-O,SC(1):LB=42:GOSUB20000
8018 IFSC(0)>9THENPOKE30520-O,30:GOTO9000:ELSEIFSC(1)>9THENPOKE30520-O,145:GOTO9000
8020 GOSUB10200:VX=0-VX:RETURN
9000 POKE30521-O,30:TX$="THE WINNER !":V=VARPTR(TX$):POKE30569-O,PEEK(V):POKE30567-O,PEEK(V+1):POKE30568-O,PEEK(V+2):LB=39:GOSUB20000
9010 CLS:PRINTCHR$(23)"PRESS ENTER FOR MENU":A$=INKEY$
9020 A$=INKEY$:IFA$=CHR$(13)THENRUNELSEGOTO9020
9999 STOP
10000 ' CLEAR COLOR SCREEN
10010 LB=9:GOSUB20000:RETURN
10100 ' DRAW LINE
10110 LB=33:GOSUB20000:RETURN
10200 ' SOUND ROUTINE
10210 LB=45:GOSUB20000:RETURN
10500 ' CLEAR SPRITES
10510 LB=30:GOSUB20000:RETURN
20000 'ACCESS USR ROUTINE
20005 ONERRORGOTO61000
20006 IFTM*256+LB<32768THENDEFUSR=TM*256+LB:GOTO20011
20010 DEFUSR=TM*256+LB-65536
20011 ONERRORGOTO0:GOTO20030
20015 ONERRORGOTO0
20020 POKE16526,LB:POKE16527,TM
20030 X7=USR(0):RETURN
30000 BC=7:FC=6:POKE30518-O,FC*16:POKE30519-O,7:LB=9:GOSUB20000:LB=30:GOSUB20000
30002 DIMC$(15):C$(1)="BLACK":C$(2)="MEDIUM GREEN":C$(3)="LIGHT GREEN":C$(4)="DARK BLUE":C$(5)="LIGHT BLUE":C$(6)="DARK RED":C$(7)="CYAN":C$(8)="MEDIUM RED":C$(9)="LIGHT RED":C$(10)="DARK YELLOW":C$(11)="LIGHT YELLOW":C$(12)="DARK GREEN"
30003 C$(13)="MAGENTA":C$(14)="GREY":C$(15)="WHITE"
30005 CLS:PRINTCHR$(23)"ENTER CHOICE:     1) CLEAR SCREEN    2) SET BACKGROUND COLOR    3) CHANGE FOREGROUND COLOR    4) DRAW STRAIGHT LINE    5) SKETCH    6) WRITE TEXT     7) RETURN TO MENUFOREGROUND COLOR IS "C$(FC)"BACKGROUND COLOR IS "C$(BC)
30010 A$=INKEY$:IFA$=""THEN30010ELSEONVAL(A$)GOTO30100,30400,30600,30800,31000,31100,30050:CLS:GOTO30005
30050 RUN
30100 INPUT"ARE YOU SURE (Y,N)";A$:IFA$="N"THEN30005
30105 CLS
30110 POKE30518-O,FC*16:POKE30519-O,BC:LB=9:GOSUB20000:LB=30:GOSUB20000:GOTO30005
30400 CLS:PRINTCHR$(23):INPUT"ENTER BACKGROUND COLOR(1-15)";BC:IFBC<1ORBC>15THEN30400ELSEXX=INP(121):OUT121,BC:OUT121,135:GOTO30005
30600 CLS:PRINTCHR$(23):INPUT"ENTER FOREGROUND COLOR(1-15)";FC:IFFC<1ORFC>15THEN30600ELSEPOKE30518-O,FC*16:GOTO30005
30800 CLS:PRINT:INPUT"ENTER STARTING POINT OF LINE(X,Y)";X,Y:POKE30522-O,X:POKE30524-O,Y
30810 INPUT"ENTER ENDING POINT OF LINE(X,Y)";X,Y:POKE30526-O,X:POKE30527-O,Y
30820 LB=33:GOSUB20000:PRINT:PRINT"ANOTHER LINE (Y,N)"
30830 A$=INKEY$:IFA$=""THEN30830ELSEIFA$="N"THEN30005ELSE30800
31000 CLS:PRINT"ENTER:    1) TRACE MODE    2) RUBBER-LINE MODE        3) SET X,Y    4) EXIT":PRINT:PRINT"BACKGROUND COLOR IS "C$(BC)"FOREGROUND COLOR IS "C$(FC)
31010 A$=INKEY$:IFA$=""THEN31010ELSEONVAL(A$)GOTO31050,31060,31080,30005:GOTO31010
31050 CLS:PRINT"PRESS ENTER TO RETURN TO MENU.":PRINT"USE A JOYSTICK OR ARROW KEYS TO MOVE TRACE AROUND SCREEN.":PRINT"PRESS FIRE BUTTON OR SPACE BAR TO DRAW LINE.":PRINT:PRINT:PRINT:PRINT"PRESS ENTER FOR MENU"
31055 IF(JSAND16)THENFL=1
31057 GOSUB40000:PRINT@448,"X="X,"Y="Y;:IFINKEY$=CHR$(13)THEN31000ELSE31055
31060 CLS:PRINT"FIRST MOVE TRACER TO LINE START AND PRESS FIRE (SPACE).":PRINT"SECOND MOVE TO LINE END AND PRESS FIRE (SPACE) TO DRAW LINE.":PRINT:PRINT:PRINT:PRINT"PRESS ENTER FOR MENU"
31065 GOSUB40000:IF(JSAND16)THENPRINT@448,"START X="X,"START Y="Y;:X1=X:Y1=Y:ELSEPRINT@384,"X="X,"Y="Y;:IFINKEY$=CHR$(13)THEN31000ELSE31065
31068 GOSUB40000:IF(JSAND16)THEN31070ELSEIFINKEY$=CHR$(13)THEN31000ELSEPRINT@384,"X="X,"Y="Y;:GOTO31068
31070 POKE30522-O,X1:POKE30524-O,Y1:POKE30526-O,X:POKE30527-O,Y:LB=33:GOSUB20000:GOTO31068
31080 CLS:PRINT:INPUT"ENTER COORDINATES (X,Y)";X,Y:IFX>255ORY>191ORX<0ORY<0THEN31080
31085 POKE30520-O,X:POKE30521-O,Y:LB=12:GOSUB20000
31090 GOTO31000
31100 CLS:PRINT:INPUT"ENTER TEXT TO PRINT:";TX$:INPUT"ENTER STARTING LOCATION OF TEXT(X,Y)";X,Y:V=VARPTR(TX$):POKE30567-O,PEEK(V+1):POKE30568-O,PEEK(V+2):POKE30569-O,PEEK(V)
31110 POKE30520-O,X:POKE30521-O,Y:LB=39:GOSUB20000:PRINT:PRINT"ANOTHER (Y,N)?"
31120 A$=INKEY$:IFA$=""THEN31120ELSEIFA$="N"THEN30005ELSE31100
40000 IFFL=0THENPOKE30520-O,X:POKE30521-O,Y:LB=15:GOSUB20000:ELSEFL=0
40001 GOSUB50000:IF(JS AND 8)THENY=Y-1:IFY<0THENY=0
40010 IF(JSAND4)THENY=Y+1:IFY>191THENY=191
40020 IF(JSAND1)THENX=X+1:IFX>255THENX=255
40030 IF(JSAND2)THENX=X-1:IFX<0THENX=0
40040 POKE30520-O,X:POKE30521-O,Y:LB=12:GOSUB20000:RETURN
50000 POKE30573-O,0:LB=51:GOSUB20000:JS=PEEK(30573-O):RETURN
60000 CLS:PRINTCHR$(23);
60010 CLEAR100:DEFINTA-N,P-Z
60020 IFPEEK(30634)=49THENPRINT"16K";:TM=119:O=0:GOSUB60072
60022 IFPEEK(45226-65536)=51THENPRINT"32K";:TM=176:O=50944:GOSUB60072
60024 IFPEEK(61610-65536)=52THENPRINT"48K";:TM=240:O=34560:GOSUB60072
60030 IFPEEK(45240-65536)=51THENPRINT"32K";:TM=176:O=50944:GOSUB60075
60040 IFPEEK(61624-65536)=52THENPRINT"48K";:TM=240:O=34560:GOSUB60075
60065 CLS
60070 PRINT"PLEASE LOAD THE CHROMATRS COLOR DRIVER FIRST!!!":GOTO60070
60072 IFPEEK(30639-O)=97THENPRINT" CASSETTE";:GOTO60080ELSECLS:PRINTCHR$(23);:RETURN
60075 IFPEEK(30653-O)=105THENPRINT" DISKETTE";ELSECLS:PRINTCHR$(23);:RETURN
60080 PRINT" COLOR DRIVER LOADED"
60090 GOTO10
60220 PRINT:PRINT"PLEASE TURN YOUR COMPUTER ON THEN OFF AND THEN RELOAD THE"
60230 PRINT"CHROMATRS COLOR DRIVER!"
60240 END
61000 RESUME 20015
