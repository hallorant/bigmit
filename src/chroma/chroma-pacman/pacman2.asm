DSKBUF	EQU	0FF00H
CLRS	EQU	9000H	;COLORS FOR BOARD
BUFF1	EQU	0A800H	;BOARD PLAYER 1
BUFF2	EQU	0C000H	;BOARD PLAYER 2
BUFF3	EQU	0D800H	;MASTER BOARD
	ORG	7000H
BLD	MACRO	#B1,#B2,#B3,#B4,#B5,#B6,#B7,#B8
	DEFB	#B1
	DEFB	#B2
	DEFB	#B3
	DEFB	#B4
	DEFB	#B5
	DEFB	#B6
	DEFB	#B7
	DEFB	#B8
	ENDM
	INCLUDE	GOBLINS.ASM
RAMIN	RES	6,H
	LD	C,79H
	OUT	(C),L
	OUT	(C),H
	NOP
	IN	A,(78H)
	RET
RAMOUT	SET	6,H
	EX	AF,AF'
	CALL	SETRAM
	EX	AF,AF'
	OUT	(78H),A
	RET
SETRAM	LD	A,L
	OUT	(79H),A
	LD	A,H
	OUT	(79H),A
	RET
REGOUT	OUT	(79H),A
	LD	A,B
	OR	80H
	OUT	(79H),A
	RET
MODE2	LD	HL,MODE2T
	LD	B,6
MODE2A	LD	A,(HL)
	CALL	REGOUT
	INC	HL
	DEC	B
	JP	P,MODE2A
	RET
MODE2T	DEFB	7
	DEFB	56
	DEFB	3
	DEFB	255
	DEFB	6
	DEFB	0C2H
	DEFB	2
CLEAR	XOR	A
	LD	HL,1800H
	CALL	RAMOUT
	LD	B,3
CLEAR1	INC	A
	OUT	(78H),A
	JP	NZ,CLEAR1
	DJNZ	CLEAR1
	LD	HL,0
	CALL	RAMOUT
	LD	HL,1800H
	LD	BC,1
CLEAR2	OUT	(78H),A
	SBC	HL,BC
	JP	NZ,CLEAR2
	LD	HL,2000H
	XOR	A
	CALL	RAMOUT
	LD	HL,1800H
	LD	BC,1
CLEAR3	OUT	(78H),A
	SBC	HL,BC
	JP	NZ,CLEAR3
	XOR	A
	LD	B,7
	JP	REGOUT
WRSTR	EX	(SP),IX
	CALL	WRSTR1
	EX	(SP),IX
	RET
WRSTR1	LD	L,(IX)
	LD	H,(IX+1)
	INC	IX
	INC	IX
WRSTR2	LD	A,(IX)
	INC	IX
	OR	A
	RET	Z
	CP	1
	JR	Z,WRSTR1
	SUB	21H
	PUSH	HL
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,CHARS
	ADD	HL,DE
	EX	DE,HL
	POP	HL
	LD	B,8
WRSTR3	LD	A,(DE)
	CALL	RAMOUT
	SET	5,H
	LD	A,C
	CALL	RAMOUT
	RES	5,H
	INC	HL
	INC	DE
	DJNZ	WRSTR3
	JP	WRSTR2
INPUT	DEFB	0	;INP CONFIG
PLAYER	DEFB	0	;NUMPLAYERS
NUMBYT	EQU	14
INTIAL	DEFB	2	;NUMBER MEN
	DEFB	0	;LEVEL
	DEFW	0	;SCORE
	DEFW	0
	DEFW	0
	DEFB	0	;OLDCH
	DEFW	0	;ENERS
	DEFW	0
	DEFB	0	;NODOT
PLAY1	DEFW	0
SCOR1	DEFS	6
	DEFB	0
	DEFW	0
	DEFW	0
	DEFB	0
PLAY2	DEFW	0
SCOR2	DEFS	6
	DEFB	0
	DEFW	0
	DEFW	0
	DEFB	0
NUMMEN	DEFB	0	;CUR # MEN
LEVEL	DEFB	0	;CUR LEV
SCORE	DEFS	6
OLDCH	DEFB	0
ENERS	DEFS	4
NODOT	DEFB	0
CURPLY	DEFB	0	;PLAYER #
YOURX	DEFW	0
YOURY	DEFW	0
MOUTH	DEFB	0
MOUTHI	DEFB	0
DIRECT	DEFB	0
MOUCON	DEFB	0
ENRCON	DEFB	0
BLNKC	DEFB	0
FRUITC	DEFW	0
FRUITD	DEFW	0
PLYCNF	DEFW	0
DCB	DEFM	'PACSCRN/PIC'
	DEFB	0DH
	DEFS	40
ENTRY	DI
	LD	A,10H
	OUT	(0ECH),A
	LD	SP,CHARS
	LD	DE,DCB
	LD	B,0
	LD	HL,DSKBUF
	CALL	4424H
	LD	DE,BUFF3
	LD	B,18H
DSKRD1	PUSH	DE
	LD	DE,DCB
	CALL	4436H
	POP	DE
	LD	HL,DSKBUF
	PUSH	BC
	LD	BC,256
	LDIR
	POP	BC
	DJNZ	DSKRD1
	LD	DE,CLRS
	LD	B,18H
DSKRD2	PUSH	DE
	LD	DE,DCB
	CALL	4436H
	POP	DE
	LD	HL,DSKBUF
	PUSH	BC
	LD	BC,256
	LDIR
	POP	BC
	DJNZ	DSKRD2
	LD	DE,DCB
	CALL	4428H
	LD	A,0C9H
	LD	(4012H),A
	DI
START	CALL	MODE2
	CALL	CLEAR
	CALL	INTSPR
	CALL	INTRO
	CALL	CLEAR
	LD	HL,BUFF3
	LD	DE,BUFF1
	LD	BC,1800H
	LDIR
	LD	HL,BUFF3
	LD	DE,BUFF2
	LD	BC,1800H
	LDIR
	LD	HL,INTIAL
	LD	DE,PLAY1
	LD	BC,NUMBYT
	LDIR
	LD	HL,INTIAL
	LD	DE,PLAY2
	LD	BC,NUMBYT
	LDIR
	XOR	A
	LD	(CURPLY),A
PLAYIT	LD	A,(CURPLY)
	LD	HL,PLAY1
	OR	A
	JR	Z,PLAYI1
	LD	HL,PLAY2
PLAYI1	LD	DE,NUMMEN
	LD	BC,NUMBYT
	LDIR
PLAYIK	LD	SP,CHARS
	LD	HL,1C00H
	LD	A,192
	CALL	RAMOUT
	LD	A,(CURPLY)
	LD	DE,BUFF1
	OR	A
	JR	Z,PLAYI2
	LD	DE,BUFF2
PLAYI2	LD	HL,4000H
	CALL	SETRAM
	LD	BC,18H
	EX	DE,HL
PLAYI3	LD	A,(HL)
	OUT	(78H),A
	INC	HL
	DJNZ	PLAYI3
	DEC	C
	JP	NZ,PLAYI3
	LD	HL,6000H
	CALL	SETRAM
	LD	BC,18H
	LD	HL,CLRS
PLAYI4	LD	A,(HL)
	OUT	(78H),A
	INC	HL
	DJNZ	PLAYI4
	DEC	C
	JP	NZ,PLAYI4
	LD	HL,0
	LD	(FRUITC),HL
	LD	(FRUITD),HL
	LD	A,(CURPLY)
	LD	E,A
	LD	D,0
	LD	HL,PLYCNF
	ADD	HL,DE
	LD	A,(HL)
	LD	(INPUT),A
	LD	HL,(LEVEL)
	LD	H,0
	LD	D,H
	LD	E,L
	ADD	HL,HL
	ADD	HL,DE
	ADD	HL,HL
	EX	DE,HL
	LD	IY,GOBDAT
	ADD	IY,DE
	CALL	PUTDAT	;PUT SCORE/NUM MEN
	CALL	MAKGOB	;MAKE GHOSTS' POSITIONS
	CALL	PUTGOB	;PUT GHOSTS ON SCREEN
	CALL	READY	;INIT YOU AND PRINT READY
;MASTER PLAYING LOOP
PLAYLP	EQU	$
	CALL	BLNKEN	;BLINK ENERGIZERS
	CALL	CHKDOT	;SEE IF YOU'RE ON A DOT
	CALL	PUTYOU	;PLACE YOU
	CALL	CHKDED	;SEE IF GHOST ON YOU
	CALL	MOVGOB	;MOVE GHOSTS
	CALL	PUTGOB	;PLACE GHOSTS
	CALL	FRUIT
	JP	PLAYLP
PUTDAT	LD	IX,SCOR1
	LD	DE,10H
	CALL	PUTSC
	LD	A,(PLAYER)
	OR	A
	JR	Z,PUTDTA
	LD	IX,SCOR2
	LD	DE,0C0H
	CALL	PUTSC
PUTDTA	LD	A,(NUMMEN)
	OR	A
	RET	Z
	CP	0FFH
	RET	Z
	CP	5
	JR	C,PUTDTL
	LD	A,4
PUTDTL	LD	B,A
	LD	HL,1610H
PUTDT1	LD	DE,PACRT
	PUSH	BC
	LD	B,16
PUTDT2	LD	A,(DE)
	CALL	RAMOUT
	INC	HL
	INC	DE
	DJNZ	PUTDT2
	LD	BC,240
	ADD	HL,BC
	LD	B,16
PUTDT3	LD	A,(DE)
	CALL	RAMOUT
	INC	HL
	INC	DE
	DJNZ	PUTDT3
	LD	BC,-256
	ADD	HL,BC
	POP	BC
	DJNZ	PUTDT1
	RET
PUTSC	LD	B,6
PUTSC1	PUSH	BC
	LD	A,(IX)
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	BC,CHARS
	ADD	HL,BC
	EX	DE,HL
	LD	B,8
PUTSC2	LD	A,(DE)
	CALL	RAMOUT
	INC	HL
	INC	DE
	DJNZ	PUTSC2
	EX	DE,HL
	INC	IX
	POP	BC
	DJNZ	PUTSC1
	RET
INTSPR	LD	BC,1184
	LD	HL,7800H
	CALL	SETRAM
	LD	HL,SPRDEF
INTSP1	LD	A,(HL)
	OUT	(78H),A
	INC	HL
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,INTSP1
	LD	HL,5C00H
	LD	B,32
	CALL	SETRAM
INTSP2	LD	A,192
	OUT	(78H),A
	XOR	A
	OUT	(78H),A
	OUT	(78H),A
	OUT	(78H),A
	DJNZ	INTSP2
	RET
READY	LD	A,122
	LD	(YOURX+1),A
	LD	A,3
	LD	(DIRECT),A
	LD	A,111
	LD	(YOURY+1),A
	DEC	A
	LD	HL,1C00H
	CALL	RAMOUT
	LD	A,122
	OUT	(78H),A
	XOR	A
	OUT	(78H),A
	LD	(MOUCON),A
	LD	(YOURX),A
	LD	(YOURY),A
	LD	(ENRCON),A
	LD	(BLNKC),A
	LD	A,10
	OUT	(78H),A
	LD	A,3
	LD	(MOUTHI),A
	LD	A,(PLAYER)
	OR	A
	JR	Z,READY1
	LD	A,(CURPLY)
	ADD	A,22H
	LD	(READY2),A
	LD	C,30H
	CALL	WRSTR
	DEFW	1758H
	DEFM	';7,D0=+'
READY2	DEFB	'x'
	DEFB	0
	LD	BC,0
	CALL	60H
	LD	C,0
	CALL	WRSTR
	DEFW	1758H
	DEFM	'++++++++'
	DEFB	0
	LD	BC,8000H
	CALL	60H
READY1	LD	C,70H
	CALL	WRSTR
	DEFW	1768H
	DEFM	'=0,/D'
	DEFB	0
	LD	BC,0
	CALL	60H
	CALL	60H
	LD	C,0A0H
	CALL	WRSTR
	DEFW	1768H
	DEFM	'+++++'
	DEFB	0
	RET
PUTYOU	LD	A,(DIRECT)
	LD	B,A
	SLA	A
	ADD	A,B
	LD	B,A
	LD	A,(MOUTH)
	CP	0FFH
	JR	Z,PUTYO1
	ADD	A,B
	INC	A
	SLA	A
	SLA	A
	LD	B,A
	JR	PUTYO2
PUTYO1	LD	B,0
PUTYO2	LD	HL,5C00H
	CALL	SETRAM
	LD	A,(YOURY+1)
	DEC	A
	OUT	(78H),A
	LD	A,(YOURX+1)
	OUT	(78H),A
	LD	A,B
	OUT	(78H),A
	RET
MVMOUT	LD	A,(MOUTHI)
	LD	E,A
	LD	D,0
	LD	HL,MOTTAB
	ADD	HL,DE
	LD	A,(HL)
	LD	(MOUTH),A
	LD	A,(MOUCON)
	XOR	1
	LD	(MOUCON),A
	RET	Z
	LD	A,(MOUTHI)
	INC	A
	CP	6
	JR	C,MVMOU1
	XOR	A
MVMOU1	LD	(MOUTHI),A
	RET
MOTTAB	DEFB	0
	DEFB	1
	DEFB	2
	DEFB	0FFH
	DEFB	2
	DEFB	1
XAMT	EQU	240
YAMT	EQU	240
MOVTAB	DEFW	0
	DEFW	-YAMT
	DEFW	XAMT
	DEFW	0
	DEFW	0
	DEFW	YAMT
	DEFW	-XAMT
	DEFW	0
MOVYOU	CALL	GETDIR
	OR	A
	JR	Z,MOVY1
	PUSH	AF
	LD	A,(YOURX+1)
	LD	D,A
	LD	A,(YOURY+1)
	LD	E,A
	CALL	SEARCH
	POP	DE
	JP	NZ,MOVY3
	LD	A,D
	AND	B
	JR	Z,MOVY4
MOVY5	LD	D,A
	XOR	A
	BIT	0,D
	JR	NZ,MOVYA
	INC	A
	BIT	1,D
	JR	NZ,MOVYA
	INC	A
	BIT	2,D
	JR	NZ,MOVYA
	INC	A
MOVYA	LD	(DIRECT),A
	JR	MOVYIT
MOVY1	LD	A,(YOURX+1)
	LD	D,A
	LD	A,(YOURY+1)
	LD	E,A
	CALL	SEARCH
	JR	NZ,MOVYIT
MOVY4	LD	C,B
	LD	A,(DIRECT)
	LD	B,A
	INC	B
	XOR	A
	SCF
MOVY2	RLA
	DJNZ	MOVY2
	AND	C
	RET	Z
MOVYIT	LD	A,(DIRECT)
	SLA	A
	SLA	A
	LD	E,A
	LD	D,0
	LD	IX,MOVTAB
	ADD	IX,DE
	LD	HL,(YOURX)
	LD	E,(IX)
	LD	D,(IX+1)
	ADD	HL,DE
	LD	(YOURX),HL
	LD	HL,(YOURY)
	LD	E,(IX+2)
	LD	D,(IX+3)
	ADD	HL,DE
	LD	(YOURY),HL
	JP	MVMOUT
MOVY3	LD	A,(DIRECT)
	LD	H,0
	LD	L,A
	LD	BC,ALTTAB
	ADD	HL,BC
	LD	A,(HL)
	AND	D
	JP	Z,MOVYIT
	JP	MOVY5
ALTTAB	DEFB	4
	DEFB	8
	DEFB	1
	DEFB	2
ALTDIR	DEFB	2
	DEFB	3
	DEFB	0
	DEFB	1
SEARCH	LD	IX,TURNS
	LD	B,51
SEARC1	LD	A,(IX)
	CP	D
	JR	NZ,SEARC2
	LD	A,(IX+1)
	CP	E
	JR	NZ,SEARC2
	LD	B,(IX+2)
	RET
SEARC2	INC	IX
	INC	IX
	INC	IX
	DJNZ	SEARC1
	OR	1
	RET
GETDIR	LD	A,(INPUT)
	OR	A
	JP	Z,KEYBD
	DEC	A
	LD	C,7DH
	JR	Z,GETDI1
	INC	C
GETDI1	IN	A,(C)
	CPL
	LD	B,A
	XOR	A
	BIT	3,B
	JR	Z,GETDR1
	OR	1
GETDR1	BIT	0,B
	JR	Z,GETDR2
	OR	2
GETDR2	BIT	2,B
	JR	Z,GETDR3
	OR	4
GETDR3	BIT	1,B
	RET	Z
	OR	8
	RET
KEYBD	LD	A,(3840H)
	LD	B,A
	XOR	A
	BIT	3,B
	JR	Z,KEYBD1
	OR	1
KEYBD1	BIT	6,B
	JR	Z,KEYBD2
	OR	2
KEYBD2	BIT	4,B
	JR	Z,KEYBD3
	OR	4
KEYBD3	BIT	5,B
	RET	Z
	OR	8
	RET
CALCU	LD	A,E
	AND	0F8H
	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	LD	A,D
	RRA
	RRA
	RRA
	AND	1FH
	ADD	A,L
	LD	L,A
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	B,0
	LD	A,E
	AND	7
	LD	C,A
	ADD	HL,BC
	LD	A,D
	CPL
	AND	7
	SLA	A
	SLA	A
	SLA	A
	LD	B,A
	CALL	RAMIN
	RET
TEST	CALL	CALCU
	EX	AF,AF'
	LD	A,B
	OR	47H
	LD	(TEST1+1),A
	EX	AF,AF'
TEST1	BIT	0,A
	RET
RESET	CALL	CALCU
	EX	AF,AF'
	LD	A,B
	OR	87H
	LD	(RESET1+1),A
	EX	AF,AF'
RESET1	RES	0,A
	JP	RAMOUT
CHKDOT	LD	A,(YOURX+1)
	ADD	A,7
	LD	D,A
	LD	A,(YOURY+1)
	ADD	A,7
	LD	E,A
	CALL	TEST
	JP	Z,MOVYOU
	LD	A,(YOURX+1)
	CP	36
	JP	Z,HTENE1
	CP	208
	JP	Z,HTENE2
	LD	A,(YOURY+1)
	CP	91
	JP	Z,HTFRUT
CHKDTA	DEC	D
	DEC	E
	CALL	RESET
	INC	D
	CALL	RESET
	INC	D
	CALL	RESET
	INC	E
	CALL	RESET
	DEC	D
	CALL	RESET
	DEC	D
	CALL	RESET
	INC	E
	CALL	RESET
	INC	D
	CALL	RESET
	INC	D
	CALL	RESET
	LD	H,60
CHKDT1	LD	A,95H
	OUT	(7EH),A
	LD	B,H
	DJNZ	$
	LD	A,80H
	OUT	(7EH),A
	LD	B,H
	DJNZ	$
	DEC	H
	JR	NZ,CHKDT1
	LD	HL,PNT5+5
	CALL	ADDSC
	LD	A,(NODOT)
	INC	A
	LD	(NODOT),A
	CP	145
	RET	NZ
	XOR	A
	LD	(NODOT),A
	LD	HL,5C02H
	CALL	SETRAM
	XOR	A
	OUT	(78H),A
	LD	HL,1C04H
	LD	B,3
	LD	A,192
	LD	DE,4
CHKDTB	CALL	RAMOUT
	ADD	HL,DE
	DJNZ	CHKDTB
	LD	BC,0
	CALL	60H
	CALL	60H
	CALL	FLASHB
	LD	A,(LEVEL)
	INC	A
	CP	11
	JR	Z,CHKDTL
	LD	(LEVEL),A
CHKDTL	LD	HL,BUFF3
	LD	DE,BUFF1
	LD	A,(CURPLY)
	OR	A
	JR	Z,CHKDT2
	LD	DE,BUFF2
CHKDT2	LD	BC,1800H
	LDIR
	LD	HL,ENERS
	LD	B,4
CHKDT3	LD	(HL),0
	INC	HL
	DJNZ	CHKDT3
	JP	PLAYIK
ADDSC	LD	IX,SCORE+5
	LD	B,6
	LD	C,0
ADDSC1	LD	A,(IX)
	ADD	A,(HL)
	ADD	A,C
	LD	C,0
	CP	0AH
	JR	C,ADDSC2
	SUB	0AH
	LD	C,1
ADDSC2	LD	(IX),A
	DEC	IX
	DEC	HL
	DJNZ	ADDSC1
	LD	IX,SCORE
	LD	DE,10H
	LD	A,(CURPLY)
	OR	A
	JR	Z,ADDSC3
	LD	DE,0C0H
ADDSC3	CALL	PUTSC
	LD	A,(SCORE+1)
	LD	B,A
	LD	A,(OLDCH)
	CP	B
	RET	Z
	LD	A,B
	LD	(OLDCH),A
	LD	A,(NUMMEN)
	INC	A
	LD	(NUMMEN),A
	LD	DE,4848H
EXTRA	LD	A,8FH
	OUT	(7EH),A
	LD	B,D
	DJNZ	$
	LD	A,80H
	OUT	(7EH),A
	LD	B,E
	DJNZ	$
	INC	D
	DEC	E
	JR	NZ,EXTRA
	JP	PUTDTA
PNT5	DEFW	0
	DEFW	0
	DEFW	500H
	DEFB	0
PNT124	DEFW	0
	DEFW	100H
	DEFW	0
HTENE1	LD	C,0
	JR	HTENE3
HTENE2	LD	C,2
HTENE3	LD	A,(YOURY+1)
	CP	47
	JP	C,CHKDTA
	CP	131
	JP	NC,CHKDTA
	CP	71
	JR	C,HTENE4
	CP	103
	JP	C,CHKDTA
	INC	C
HTENE4	LD	B,0
	LD	HL,ENERS
	ADD	HL,BC
	LD	(HL),1
	CALL	ERASEN
	LD	HL,PNT5+6
	CALL	ADDSC
	LD	H,0A0H
	LD	C,40
HTENE5	LD	A,98H
	OUT	(7EH),A
	LD	B,H
	DJNZ	$
	LD	A,80H
	OUT	(7EH),A
	LD	B,H
	DJNZ	$
	INC	H
	DEC	C
	JR	NZ,HTENE5
	LD	C,40
HTENE6	LD	A,98H
	OUT	(7EH),A
	LD	B,H
	DJNZ	$
	LD	A,80H
	OUT	(7EH),A
	LD	B,H
	DJNZ	$
	DEC	H
	DEC	C
	JR	NZ,HTENE6
	LD	A,1
	LD	(PNT124+3),A
	LD	IX,GOBTAB
	LD	B,3
HTENEG	LD	A,(IY)
	LD	(IX+11),A
	LD	A,(IY+1)
	LD	(IX+12),A
	LD	(IX+14),0FFH
	LD	DE,15
	ADD	IX,DE
	DJNZ	HTENEG
	JP	MOVYOU
HTFRUT	LD	A,(YOURX+1)
	CP	112
	JP	C,CHKDTA
	CP	134
	JP	NC,CHKDTA
	CALL	ERFRUT
	LD	HL,0
	LD	(FRUITC),HL
	LD	(FRUITD),HL
	LD	A,(LEVEL)
	LD	B,A
	SLA	A
	ADD	A,B
	SLA	A
	LD	E,A
	LD	D,0
	LD	HL,FRTPNT+5
	ADD	HL,DE
	CALL	ADDSC
	LD	C,4
FRTSN1	LD	L,236
FRTSN2	LD	A,31
	OUT	(7EH),A
	LD	B,L
	DJNZ	$
	XOR	A
	OUT	(7EH),A
	LD	B,L
	DJNZ	$
	DEC	L
	DEC	L
	LD	A,L
	CP	200
	JR	NZ,FRTSN2
	DEC	C
	JR	NZ,FRTSN1
	JP	MOVYOU
ERASEN	LD	A,C
	CP	2
	JR	C,ERASN1
	LD	HL,47D0H
	CP	2
	JR	Z,ERASI
	LD	H,4EH
ERASI	CALL	ERASN2
	LD	A,L
	ADD	A,8
	LD	L,A
	JR	ERASN2
ERASN1	LD	HL,4728H
	OR	A
	JR	Z,ERASN2
	LD	H,4EH
ERASN2	CALL	SETRAM
	LD	B,8
	XOR	A
ERASN3	OUT	(78H),A
	DJNZ	ERASN3
	RET
BLNKEN	LD	A,(BLNKC)
	INC	A
	AND	0FH
	LD	(BLNKC),A
	RET	NZ
	LD	A,(ENRCON)
	INC	A
	CP	6
	JR	C,BLNKE1
	XOR	A
BLNKE1	LD	(ENRCON),A
	LD	C,A
	LD	B,0
	LD	HL,COLTAB
	ADD	HL,BC
	LD	C,(HL)
	LD	HL,6728H
	CALL	FILCOL
	LD	HL,6E28H
	CALL	FILCOL
	LD	HL,67D0H
	CALL	FILCOL
	LD	HL,67D8H
	CALL	FILCOL
	LD	HL,6ED0H
	CALL	FILCOL
	LD	HL,6ED8H
FILCOL	CALL	SETRAM
	LD	B,8
	LD	A,C
FILCO1	OUT	(78H),A
	DJNZ	FILCO1
	RET
COLTAB	DEFB	20H
	DEFB	10H
	DEFB	50H
	DEFB	10H
	DEFB	90H
	DEFB	10H
FLASHB	LD	C,0
FLASB0	CALL	ERASEN
	INC	C
	BIT	2,C
	JR	Z,FLASB0
	CALL	ERFRUT
	LD	B,3
FLASB1	PUSH	BC
	LD	C,60H
	CALL	FLASB2
	LD	C,50H
	CALL	FLASB2
	POP	BC
	DJNZ	FLASB1
	RET
FLASB2	LD	HL,6100H
	CALL	SETRAM
	LD	A,C
	LD	BC,0015H
FLASB3	OUT	(78H),A
	DJNZ	FLASB3
	DEC	C
	JP	NZ,FLASB3
	LD	BC,0A000H
	CALL	60H
	RET
PUTGOB	LD	IX,GOBTAB
	LD	B,3
	LD	HL,1C04H
PTGOB1	PUSH	BC
	LD	A,(IX+4)
	DEC	A
	CALL	RAMOUT
	LD	A,(IX+2)
	OUT	(78H),A
	LD	A,(IX+13)
	INC	A
	AND	3
	LD	(IX+13),A
	SLA	A
	SLA	A
	PUSH	HL
	LD	L,(IX+9)
	LD	H,0
	LD	DE,DIRSHF
	ADD	HL,DE
	ADD	A,(HL)
	POP	HL
	ADD	A,13
	SLA	A
	SLA	A
	OUT	(78H),A
	LD	C,B
	DEC	C
	LD	B,0
	PUSH	HL
	LD	HL,GOBCOL
	ADD	HL,BC
	LD	C,(HL)
	POP	HL
	LD	A,(IX+11)
	OR	(IX+12)
	JR	NZ,PTGOB3
	LD	A,(IX+14)
	CP	0FFH
	JR	Z,PTGOB2
	AND	1FH
	CP	10H
	JR	C,PTGOB2
	LD	C,0FH
	JR	PTGOB2
PTGOB3	LD	C,4
PTGOB2	LD	A,C
	OUT	(78H),A
	POP	BC
	LD	DE,15
	ADD	IX,DE
	LD	DE,4
	ADD	HL,DE
	DJNZ	PTGOB1
	RET
DIRSHF	DEFB	1
	DEFB	2
	DEFB	0
	DEFB	3
GOBCOL	DEFB	7
	DEFB	12
	DEFB	8
GOBTAB	DEFS	3*15
MAKGOB	LD	HL,GOBTAB
	LD	DE,GOBTAB+1
	LD	BC,44
	LD	(HL),0
	LDIR
	LD	IX,GOBTAB
	LD	B,3
	LD	HL,GOBPAR
	PUSH	IY
	INC	IY
	INC	IY
	INC	IY
MAKGO1	LD	A,(HL)
	LD	(IX+2),A
	INC	HL
	LD	A,(HL)
	LD	(IX+4),A
	INC	HL
	LD	A,(HL)
	LD	(IX+9),A
	INC	HL
	LD	A,(HL)
	LD	(IX+10),A
	INC	HL
	LD	A,(IY)
	LD	(IX),A
	LD	(IX+14),0FFH
	INC	IY
	LD	DE,15
	ADD	IX,DE
	DJNZ	MAKGO1
	POP	IY
	RET
GOBPAR	DEFB	122
	DEFB	73
	DEFB	0
	DEFB	0A5H
	DEFB	97
	DEFB	73
	DEFB	1
	DEFB	98H
	DEFB	147
	DEFB	73
	DEFB	3
	DEFB	0E8H
GOBDAT	DEFW	350
	DEFB	254
	DEFB	0
	DEFB	4
	DEFB	8
	DEFW	300	;1
	DEFB	190
	DEFB	0
	DEFB	3
	DEFB	6
	DEFW	220	;2
	DEFB	150
	DEFB	0
	DEFB	3
	DEFB	4
	DEFW	170	;3
	DEFB	110
	DEFB	0
	DEFB	2
	DEFB	5
	DEFW	60	;4
	DEFB	70
	DEFB	0
	DEFB	3
	DEFB	3
	DEFW	1	;5
	DEFB	100
	DEFB	0
	DEFB	1
	DEFB	2
	DEFW	400	;6
	DEFB	70
	DEFB	0
	DEFB	3
	DEFB	5
	DEFW	220	;7
	DEFB	250
	DEFB	0
	DEFB	2
	DEFB	2
	DEFW	260	;8
	DEFB	1
	DEFB	0
	DEFB	1
	DEFB	1
	DEFW	100	;9
	DEFB	30
	DEFB	0
	DEFB	0
	DEFB	1
	DEFW	1	;10
	DEFB	60
	DEFB	0
	DEFB	0
	DEFB	0
MOVGOB	LD	IX,GOBTAB
	LD	B,3
MOVGB1	PUSH	BC
	LD	A,(IX+10)
	LD	(SEED),A
	LD	A,(IX)
	CP	0FFH
	JR	Z,OUTBOX
	OR	A
	JR	NZ,OUTBOX
WATOT	LD	A,(IX+2)
	CP	122
	JR	NZ,OUTBOX
	LD	(IX+9),0
	DEC	(IX)
OUTBOX	LD	A,(IX+9)
	EX	AF,AF'
	CALL	EVASE
	LD	A,(IX)
	CP	0FFH
	JR	Z,EVASEN
	EX	AF,AF'
	LD	(IX+9),A
EVASEN	LD	A,(IX+2)
	POP	BC
	PUSH	BC
	CP	16
	JP	C,INTUNN
	CP	215
	JP	NC,INTUNN
	LD	A,(IX+11)
	OR	(IX+12)
	JP	NZ,SLWBNK
	LD	A,(IX+14)
	INC	A
	JP	NZ,SLWBNK
	LD	A,3
	SUB	B
	LD	B,A
	SLA	A
	SLA	A
	ADD	A,B
	LD	B,A
	SLA	A
	ADD	A,B
	CPL
SLWBLK	LD	L,A
	LD	H,0
	LD	DE,0
	LD	A,(IX+9)
	OR	A
	JR	NZ,OUTBX1
	CALL	INVHL
	JR	OUTBX4
INTUNN	LD	A,180
	JR	SLWBLK
SLWBNK	LD	A,205
	JR	SLWBLK
OUTBX1	DEC	A
	JR	NZ,OUTBX2
	EX	DE,HL
	JR	OUTBX4
INVHL	LD	A,H
	CPL
	LD	H,A
	LD	A,L
	CPL
	LD	L,A
	INC	HL
	RET
OUTBX2	DEC	A
	JR	Z,OUTBX4
	CALL	INVHL
	EX	DE,HL
OUTBX4	LD	(IX+5),E
	LD	(IX+6),D
	LD	(IX+7),L
	LD	(IX+8),H
	LD	L,(IX+1)
	LD	H,(IX+2)
	LD	E,(IX+5)
	LD	D,(IX+6)
	ADD	HL,DE
	LD	(IX+1),L
	LD	(IX+2),H
	LD	L,(IX+3)
	LD	H,(IX+4)
	LD	E,(IX+7)
	LD	D,(IX+8)
	ADD	HL,DE
	LD	(IX+3),L
	LD	(IX+4),H
	LD	A,(IX)
	INC	A
	JR	Z,OUTBX6
	LD	A,(IX+2)
	CP	96
	JR	Z,OUTBX5
	CP	148
	JR	NZ,OUTBX6
OUTBX5	LD	A,4
	SUB	(IX+9)
	LD	(IX+9),A
	LD	A,(IX)
	OR	A
	JR	Z,OUTBX6
	DEC	(IX)
OUTBX6	LD	L,(IX+11)
	LD	H,(IX+12)
	LD	A,H
	OR	L
	JR	Z,OUTBX7
	DEC	HL
	LD	(IX+11),L
	LD	(IX+12),H
	LD	A,H
	OR	L
	JR	NZ,OUTBX9
	LD	A,(IY+2)
	LD	(IX+14),A
	JR	OUTBX9
OUTBX7	LD	A,(IX+14)
	CP	0FFH
	JR	Z,OUTBX9
	DEC	(IX+14)
OUTBX9	POP	BC
	LD	A,(SEED)
	LD	(IX+10),A
	LD	DE,15
	ADD	IX,DE
	DEC	B
	JP	NZ,MOVGB1
	RET
SEED	DEFB	0
RAND	LD	A,(SEED)
	RLCA
	PUSH	BC
	LD	B,A
	LD	A,R
	XOR	B
	LD	(SEED),A
	POP	BC
	RET
ABS	BIT	7,A
	RET	Z
	NEG
	RET
EVASE	LD	D,(IX+2)
	LD	E,(IX+4)
	PUSH	IX
	CALL	SEARCH
	POP	IX
	RET	NZ
	PUSH	BC
	LD	L,5
	LD	A,(YOURX+1)
	SUB	D
	CALL	ABS
	CP	10H
	JR	C,EVASEX
	LD	A,(YOURY+1)
	SUB	E
	CALL	ABS
	CP	10H
	JR	C,EVASEY
EVASE1	CALL	RAND
	AND	3
EVASE2	EXX
	LD	C,(IX+9)
	LD	B,0
	LD	HL,ALTDIR
	ADD	HL,BC
	CP	(HL)
	EXX
	JR	Z,EVASE4
	LD	B,A
	LD	D,A
	INC	B
	XOR	A
	SCF
EVASE3	RLA
	DJNZ	EVASE3
	POP	BC
	PUSH	BC
	AND	B
	JR	Z,EVASE4
	LD	(IX+9),D
	POP	BC
	RET
EVASE4	DEC	L
	JR	NZ,EVASE1
	POP	BC
	XOR	A
EVASE5	RR	B
	JR	C,EVASE6
EVASE7	INC	A
	JR	EVASE5
EVASE6	LD	E,(IX+9)
	LD	D,0
	LD	HL,ALTDIR
	ADD	HL,DE
	CP	(HL)
	JR	Z,EVASE7
	LD	(IX+9),A
	RET
EVASEX	LD	A,(IX+11)
	OR	(IX+12)
	JR	NZ,EVASX1
	LD	A,(IX+14)
	INC	A
	JR	NZ,EVASX1
	LD	A,(YOURY+1)
	CP	E
	LD	A,(IX+9)
	JP	Z,EVASE2
	LD	A,2
	JP	NC,EVASE2
	XOR	A
	JP	EVASE2
EVASX1	LD	A,(YOURY+1)
	CP	E
	LD	A,2
	JP	C,DNTEVS
	XOR	A
	JP	DNTEVS
EVASEY	LD	A,(IX+11)
	OR	(IX+12)
	JP	NZ,EVASY1
	LD	A,(IX+14)
	INC	A
	JR	NZ,EVASY1
	LD	A,(YOURX+1)
	CP	D
	LD	A,(IX+9)
	JP	Z,EVASE2
	LD	A,1
	JP	NC,EVASE2
	LD	A,3
	JP	EVASE2
EVASY1	LD	A,(YOURX+1)
	CP	D
	LD	A,3
	JP	NC,DNTEVS
	LD	A,1
DNTEVS	LD	L,5
	LD	C,A
DNTEV1	CALL	RAND
	AND	3
DNTEV2	EXX
	LD	C,(IX+9)
	LD	B,0
	LD	HL,ALTDIR
	ADD	HL,BC
	CP	(HL)
	EXX
	JP	Z,DNTEV5
	CP	C
	JP	Z,DNTEV5
	LD	B,A
	LD	D,A
	INC	B
	XOR	A
	SCF
DNTEV3	RLA
	DJNZ	DNTEV3
	POP	BC
	PUSH	BC
	AND	B
	JP	Z,DNTEV5
	LD	(IX+9),D
	POP	BC
	RET
DNTEV5	DEC	L
	JR	NZ,DNTEV1
	POP	BC
	XOR	A
	JP	EVASE5
CHKDED	LD	IX,GOBTAB
	LD	B,3
	PUSH	IY
	INC	IY
	INC	IY
	INC	IY
CHKDE1	LD	A,(YOURX+1)
	SUB	(IX+2)
	CALL	ABS
	CP	4
	JP	NC,CHKDE4
	LD	A,(YOURY+1)
	SUB	(IX+4)
	CALL	ABS
	CP	4
	JP	NC,CHKDE4
	LD	A,(IX+14)
	CP	0FFH
	JR	NZ,CHKDE2
	LD	A,(IX+11)
	OR	(IX+12)
	JP	Z,DEADYU
CHKDE2	LD	A,3
	SUB	B
	SLA	A
	SLA	A
	LD	E,A
	LD	D,0
	LD	HL,GOBPAR
	ADD	HL,DE
	LD	A,(HL)
	LD	(IX+2),A
	INC	HL
	LD	A,(HL)
	LD	(IX+4),A
	INC	HL
	LD	A,(HL)
	LD	(IX+9),A
	LD	A,(IY)
	LD	(IX),A
	LD	(IX+11),0
	LD	(IX+12),0
	LD	(IX+14),0FFH
	PUSH	BC
	LD	A,(PNT124+3)
	ADD	A,21H
	LD	(CHKDSA),A
	LD	C,90H
	CALL	WRSTR
	DEFW	1770H
CHKDSA	DEFM	'x!!'
	DEFB	0
	LD	HL,PNT124+5
	CALL	ADDSC
	LD	D,6
	LD	C,5
CHKDS1	LD	L,255
CHKDS2	XOR	A
	OUT	(7EH),A
	LD	B,L
	DJNZ	$
	LD	A,D
	OUT	(7EH),A
	LD	B,L
	DJNZ	$
	LD	A,L
	SUB	15
	LD	L,A
	JR	NZ,CHKDS2
	LD	A,D
	ADD	A,5
	LD	D,A
	DEC	C
	JR	NZ,CHKDS1
	LD	C,0
	CALL	WRSTR
	DEFW	1770H
	DEFM	'+++'
	DEFB	0
	LD	A,(PNT124+3)
	SLA	A
	LD	(PNT124+3),A
	POP	BC
CHKDE4	INC	IY
	LD	DE,15
	ADD	IX,DE
	DEC	B
	JP	NZ,CHKDE1
	POP	IY
	RET
DEADYU	LD	HL,1C04H
	LD	B,3
	LD	A,192
	LD	DE,4
DEADY1	CALL	RAMOUT
	ADD	HL,DE
	DJNZ	DEADY1
	CALL	ERFRUT
	LD	B,18
DEADY2	LD	A,(DIRECT)
	LD	C,A
	SLA	A
	ADD	A,C
	INC	A
	SLA	A
	SLA	A
	LD	HL,1C02H
	CALL	RAMOUT
	PUSH	BC
	LD	A,B
	SLA	A
	LD	C,A
	LD	L,50
DEADY5	LD	A,C
	OUT	(7EH),A
	LD	B,L
	DJNZ	$
	XOR	A
	OUT	(7EH),A
	LD	B,L
	DJNZ	$
	INC	L
	LD	A,L
	CP	220
	JR	NZ,DEADY5
	POP	BC
	LD	A,(DIRECT)
	INC	A
	AND	3
	LD	(DIRECT),A
	DJNZ	DEADY2
	LD	HL,256
	CALL	SETRAM
	LD	BC,15H
	LD	HL,BUFF1+256
	LD	A,(CURPLY)
	OR	A
	JR	Z,DEADY3
	LD	HL,BUFF2+256
DEADY3	IN	A,(78H)
	LD	(HL),A
	INC	HL
	DJNZ	DEADY3
	DEC	C
	JR	NZ,DEADY3
	LD	A,(NUMMEN)
	DEC	A
	LD	(NUMMEN),A
	LD	DE,PLAY1
	LD	A,(CURPLY)
	OR	A
	JR	Z,DEADY4
	LD	DE,PLAY2
DEADY4	LD	HL,NUMMEN
	LD	BC,NUMBYT
	LDIR
	LD	A,(NUMMEN)
	CP	0FFH
	JP	Z,GAMOVR
GAMNVR	LD	A,(PLAYER)
	OR	A
	JP	Z,PLAYIT
	LD	HL,PLAY1
	LD	A,(CURPLY)
	OR	A
	JR	NZ,DEADYH
	LD	HL,PLAY2
DEADYH	LD	A,(HL)
	INC	A
	JP	Z,PLAYIT
	LD	A,(CURPLY)
	XOR	1
	LD	(CURPLY),A
	JP	PLAYIT
GAMOVR	LD	A,(PLAYER)
	OR	A
	JR	Z,GAMOV1
	LD	A,(CURPLY)
	ADD	A,22H
	LD	(GAMOR1),A
	LD	C,50H
	CALL	WRSTR
	DEFW	1738H
	DEFM	'2,80+:A0=+;7,D0=+'
GAMOR1	DEFB	'x'
	DEFB	0
	LD	BC,0
	CALL	60H
	CALL	60H
	LD	A,(PLAY1)
	LD	B,A
	LD	A,(PLAY2)
	AND	B
	CP	0FFH
	JP	Z,START
	JP	GAMNVR
GAMOV1	LD	C,50H
	CALL	WRSTR
	DEFW	1758H
	DEFM	'2,80+:A0='
	DEFB	0
	LD	BC,0
	CALL	60H
	CALL	60H
	JP	START
FRUIT	LD	HL,(FRUITC)
	LD	DE,300H
	OR	A
	SBC	HL,DE
	JP	Z,FRUIT2
	LD	HL,(FRUITC)
	INC	HL
	LD	(FRUITC),HL
	LD	DE,300H
	OR	A
	SBC	HL,DE
	RET	NZ
	LD	HL,(LEVEL)
	LD	H,0
	LD	DE,FRTCOL
	ADD	HL,DE
	LD	C,(HL)
	LD	HL,(LEVEL)
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,FRUITS
	ADD	HL,DE
	EX	DE,HL
	LD	IX,FRTPOS
	LD	B,32
FRUIT1	LD	L,(IX)
	LD	H,(IX+1)
	LD	A,(DE)
	CALL	RAMOUT
	SET	5,H
	LD	A,C
	CALL	RAMOUT
	INC	IX
	INC	IX
	INC	DE
	DJNZ	FRUIT1
	LD	HL,200H
	LD	(FRUITD),HL
	RET
FRUIT2	LD	HL,(FRUITD)
	DEC	HL
	LD	(FRUITD),HL
	LD	A,H
	OR	L
	RET	NZ
ERFRUT	LD	IX,FRTPOS
	LD	B,32
	XOR	A
FRUIT5	LD	L,(IX)
	LD	H,(IX+1)
	CALL	RAMOUT
	INC	IX
	INC	IX
	DJNZ	FRUIT5
	LD	HL,0
	LD	(FRUITC),HL
	RET
FRTCOL	DEFB	0F0H
	DEFB	40H
	DEFB	0D0H
	DEFB	60H
	DEFB	20H
	DEFB	0E0H
	DEFB	60H
	DEFB	0B0H
	DEFB	0A0H
	DEFB	0B0H
	DEFB	40H
FRTPOS	DEFW	0B7BH
	DEFW	0B7CH
	DEFW	0B7DH
	DEFW	0B7EH
	DEFW	0B7FH
	DEFW	0C78H
	DEFW	0C79H
	DEFW	0C7AH
	DEFW	0C7BH
	DEFW	0C7CH
	DEFW	0C7DH
	DEFW	0C7EH
	DEFW	0C7FH
	DEFW	0D78H
	DEFW	0D79H
	DEFW	0D7AH
	DEFW	0B83H
	DEFW	0B84H
	DEFW	0B85H
	DEFW	0B86H
	DEFW	0B87H
	DEFW	0C80H
	DEFW	0C81H
	DEFW	0C82H
	DEFW	0C83H
	DEFW	0C84H
	DEFW	0C85H
	DEFW	0C86H
	DEFW	0C87H
	DEFW	0D80H
	DEFW	0D81H
	DEFW	0D82H
FRTPNT	DEFW	0	;DIAMND
	DEFW	100H
	DEFW	0
	DEFW	0	;CIRC
	DEFW	300H
	DEFW	0
	DEFW	0	;RING
	DEFW	400H
	DEFW	0
	DEFW	0	;HD
	DEFW	500H
	DEFW	0
	DEFW	0	;BF
	DEFW	700H
	DEFW	0
	DEFW	0	;MSH RM
	DEFW	1
	DEFW	0
	DEFW	0	;SB
	DEFW	0501H
	DEFW	0
	DEFW	0	;PEACH
	DEFW	2
	DEFW	0
	DEFW	0	;PRETZ
	DEFW	3
	DEFW	0
	DEFW	0	;BAN
	DEFW	4
	DEFW	0
	DEFW	0	;KEY
	DEFW	5
	DEFW	0
PRNTR	EX	(SP),HL
PRNTR1	LD	A,(HL)
	INC	HL
	OR	A
	JR	Z,PRNTR2
	CALL	33H
	JR	PRNTR1
PRNTR2	EX	(SP),HL
	RET
INTRO	CALL	PRNTR
	DEFW	1F1CH
	DEFB	15
	DEFM	'                 *** Options setting ***'
	DEFW	0D0DH
	DEFM	'Number of players: '
